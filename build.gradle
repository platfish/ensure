plugins {
    id "com.jfrog.bintray" version "1.8.0"
    id 'net.researchgate.release' version '2.6.0'
}

apply plugin: 'java'
apply plugin: 'maven'

group = 'org.platfish'
version = version

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.4'
}

Properties properties = new Properties()
properties.load(project.rootProject.file("local.properties").newDataInputStream())

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            name "Marc Ewert"
        }
    }

    scm {
        url "https://github.com/platfish/ensure.git"
    }
}

publishing {
    publications {
        mavenPublish(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.group
            artifactId project.name
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Java library for condition checking and failing fast with meaningful error messages')
                root.appendNode('name', project.name)
                root.appendNode('url', 'https://github.com/platfish/ensure')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = properties.getProperty('bintray.user')
    key = properties.getProperty('bintray.key')
    publications = ['mavenPublish']
    publish = true
    pkg {
        repo = properties.getProperty('bintray.repo')
        name = project.name
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/platfish/ensure'
        version {
            name = project.version
        }
    }
}

afterReleaseBuild.dependsOn bintrayUpload
